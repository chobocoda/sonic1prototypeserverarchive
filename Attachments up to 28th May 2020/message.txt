; ---------------------------------------------------------------------------
; Object 1F - Crabmeat enemy (GHZ, SYZ)
; ---------------------------------------------------------------------------

Crabmeat:
		moveq	#0,d0
		move.b	obRoutine(a0),d0
		move.w	Crab_Index(pc,d0.w),d1
		jmp	Crab_Index(pc,d1.w)
; ===========================================================================
Crab_Index:
ptr_Crab_Main:		dc.w Crab_Main-Crab_Index
ptr_Crab_Action:	dc.w Crab_Action-Crab_Index
ptr_Crab_Delete:	dc.w Crab_Delete-Crab_Index
ptr_Crab_BallMain:	dc.w Crab_BallMain-Crab_Index
ptr_Crab_BallMove:	dc.w Crab_BallMove-Crab_Index

id_Crab_Main:		equ ptr_Crab_Main-Crab_Index	; 0
id_Crab_Action:		equ ptr_Crab_Action-Crab_Index	; 2
id_Crab_Delete:		equ ptr_Crab_Delete-Crab_Index	; 4
id_Crab_BallMain:	equ ptr_Crab_BallMain-Crab_Index	; 6
id_Crab_BallMove:	equ ptr_Crab_BallMove-Crab_Index	; 8

crab_timedelay:	equ $30
crab_mode:	equ $32
; ===========================================================================

Crab_Main:	; Routine 0
		move.b	#$10,obHeight(a0)
		move.b	#8,obWidth(a0)
		move.l	#Map_Crab,obMap(a0)
		move.w	#$400,obGfx(a0)
		move.b	#4,obRender(a0)
		move.b	#3,obPriority(a0)
		move.b	#6,obColType(a0)
		move.b	#$15,obActWid(a0)
		bsr.w	ObjectFall
		jsr	(ObjFloorDist).l	; find floor
		tst.w	d1
		bpl.s	@floornotfound
		add.w	d1,obY(a0)
		move.b	d3,obAngle(a0)
		move.w	#0,obVelY(a0)
		addq.b	#2,obRoutine(a0)
		move.w	#127,crab_timedelay(a0)
		move.w	#$80,obVelX(a0)
		bchg	#0,obStatus(a0)
		bne.s	@right
		neg.w	obVelX(a0)
	@right:
	@floornotfound:
		rts	
; ===========================================================================

Crab_Action:	; Routine 2
		moveq	#0,d0
		move.b	ob2ndRout(a0),d0
		move.w	@index(pc,d0.w),d1
		jsr	@index(pc,d1.w)
		lea	(Ani_Crab).l,a1
		bsr.w	AnimateSprite
		bra.w	RememberState
; ===========================================================================
@index:		dc.w @walk-@index
		dc.w @waittofire-@index
		dc.w @waittowalk-@index

; ===========================================================================

@walk:
		subq.w	#1,crab_timedelay(a0)
		bmi.s	@offside
		bsr.w	SpeedToPos
		bchg	#0,crab_mode(a0)
		bne.s	@midcol
		move.w	obX(a0),d3
		addi.w	#$10,d3
		btst	#0,obStatus(a0)
		beq.s	@sidecol
		subi.w	#$20,d3

	@sidecol:
		jsr	(ObjFloorDist2).l
		cmpi.w	#-8,d1
		blt.s	@offside
		cmpi.w	#$C,d1
		bge.s	@offside
		rts	
; ===========================================================================

	@midcol:
		jsr	(ObjFloorDist).l
		add.w	d1,obY(a0)
		move.b	d3,obAngle(a0)
		move.b	#0,obAnim(a0)
		rts	
; ===========================================================================

	@offside:
		bchg	#1,crab_mode(a0)
		bne.s	@turn
		addi.b	#2,ob2ndRout(a0)
		move.w	#$1F,crab_timedelay(a0)
		move.w	#0,obVelX(a0)
		move.b	#1,obAnim(a0)
		rts
		
	@turn:
		move.w	#127,crab_timedelay(a0)
		bchg	#0,obStatus(a0)
		neg.w	obVelX(a0)
		rts

; ===========================================================================
@waittofire:
		subq.w	#1,crab_timedelay(a0) ; subtract 1 from time delay
		bpl.s	@nofire
		tst.b	obRender(a0)
		bpl.s	@retmove

; ===========================================================================

@fire:
		move.w	#29,crab_timedelay(a0)
		addi.b	#2,ob2ndRout(a0)
		bsr.w	FindFreeObj
		bne.s	@noobj
		move.b	#id_Crabmeat,0(a1) ; load left fireball
		move.b	#id_Crab_BallMain,obRoutine(a1)
		move.w	obX(a0),obX(a1)
		move.w	obY(a0),obY(a1)
		addi.w	#$C,obY(a1)

@noobj:
@nofire:
		rts

; ===========================================================================
@waittowalk:
		subq.w	#1,crab_timedelay(a0) ; subtract 1 from time delay
		bpl.s	@stillwait
@retmove:
		move.b	#0,ob2ndRout(a0)
		move.w	#127,crab_timedelay(a0)
		move.w	#$80,obVelX(a0)
		bchg	#0,obStatus(a0)
		bne.s	@right
		neg.w	obVelX(a0)
@right:
@stillwait:
		rts

; ===========================================================================
		
Crab_Delete:	; Routine 4
		bsr.w	DeleteObject
		rts	
; ===========================================================================
; ---------------------------------------------------------------------------
; Sub-object - missile that the	Crabmeat throws
; ---------------------------------------------------------------------------

Crab_BallMain:	; Routine 6
		addq.b	#2,obRoutine(a0)
		move.l	#Map_Crab,obMap(a0)
		move.w	#$400,obGfx(a0)
		move.b	#4,obRender(a0)
		move.b	#2,obPriority(a0)
		move.b	#$87,obColType(a0)
		move.b	#8,obActWid(a0)
		move.b	#2,obAnim(a0)

Crab_BallMove:	; Routine 8
		lea	(Ani_Crab).l,a1
		bsr.w	AnimateSprite
		bsr.w	ObjectFall
		bsr.w	DisplaySprite
		move.w	(v_limitbtm2).w,d0
		addi.w	#$E0,d0
		cmp.w	obY(a0),d0	; has object moved below the level boundary?
		bcs.s	@delete		; if yes, branch
		rts	

	@delete:
		bra.w	DeleteObject
